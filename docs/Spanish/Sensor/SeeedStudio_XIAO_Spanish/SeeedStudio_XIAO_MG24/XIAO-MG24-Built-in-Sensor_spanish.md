---
title: Sensor Integrado de XIAO MG24 Sense de Seeed Studio  
description: Este art铆culo describe c贸mo usar el micr贸fono en el XIAO MG24 Sense.
image: https://files.seeedstudio.com/wiki/mg24_mic/mg24.jpg
slug: /xiao_mg24_sense_built_in_sensor_spanish
keywords:
  - XIAO
  - MG24
last_update:
  date: 02/16/2025
  author: Guillermo
sidebar_position: 3
---

# Uso del Sensor Integrado de Seeed Studio XIAO MG24 Sense

## XIAO MG24 Sense IMU

### Descripci贸n general de los sensores integrados

Los sensores **IMU de 6 ejes (Unidad de Medici贸n Inercial)** como el **LSM6DS3TR-C** integran aceler贸metros y giroscopios para medir el movimiento y la orientaci贸n de un objeto en el espacio tridimensional. Espec铆ficamente, el LSM6DS3TR-C tiene las siguientes caracter铆sticas:

**Funci贸n del aceler贸metro:**
- Mide la aceleraci贸n de un objeto a lo largo de los ejes X, Y y Z. Puede detectar el movimiento del objeto (por ejemplo, reposo, aceleraci贸n, desaceleraci贸n) y los cambios en la inclinaci贸n (por ejemplo, el 谩ngulo del objeto).
- Se puede usar para detectar la marcha, cambios de posici贸n, vibraciones, etc.

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/xyz1.5.jpg" style={{width:320, height:'auto'}}/></div>

**Funci贸n del giroscopio (Giroscopio):**
- Mide la velocidad angular de un objeto alrededor de los ejes X, Y y Z, es decir, la rotaci贸n del objeto.
- Se puede usar para detectar la rotaci贸n, la tasa de rotaci贸n y el cambio de direcci贸n.

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/xyz2.0.jpg" style={{width:320, height:'auto'}}/></div>

- El **谩ngulo del eje X (Roll)** es el 谩ngulo en la direcci贸n de rotaci贸n alrededor del eje X.
- El **谩ngulo del eje Y (Pitch)** es el 谩ngulo en la direcci贸n de rotaci贸n alrededor del eje Y.
- El **谩ngulo del eje Z (Yaw)** es el 谩ngulo en la direcci贸n de rotaci贸n alrededor del eje Z.

### Preparaci贸n del software

<div class="github_container" style={{textAlign: 'center'}}>
    <a class="github_item" href="https://github.com/Seeed-Studio/Seeed_Arduino_LSM6DS3">
    <strong><span><font color={'FFFFFF'} size={"4"}> Descargar las bibliotecas</font></span></strong> <svg aria-hidden="true" focusable="false" role="img" className="mr-2" viewBox="-3 10 9 1" width={16} height={16} fill="currentColor" style={{textAlign: 'center', display: 'inline-block', userSelect: 'none', verticalAlign: 'text-bottom', overflow: 'visible'}}><path d="M8 0c4.42 0 8 3.58 8 8a8.013 8.013 0 0 1-5.45 7.59c-.4.08-.55-.17-.55-.38 0-.27.01-1.13.01-2.2 0-.75-.25-1.23-.54-1.48 1.78-.2 3.65-.88 3.65-3.95 0-.88-.31-1.59-.82-2.15.08-.2.36-1.02-.08-2.12 0 0-.67-.22-2.2.82-.64-.18-1.32-.27-2-.27-.68 0-1.36.09-2 .27-1.53-1.03-2.2-.82-2.2-.82-.44 1.1-.16 1.92-.08 2.12-.51.56-.82 1.28-.82 2.15 0 3.06 1.86 3.75 3.64 3.95-.23.2-.44.55-.51 1.07-.46.21-1.61.55-2.33-.66-.15-.24-.6-.83-1.23-.82-.67.01-.27.38.01.53.34.19.73.9.82 1.13.16.45.68 1.31 2.69.94 0 .67.01 1.3.01 1.49 0 .21-.15.45-.55.38A7.995 7.995 0 0 1 0 8c0-4.42 3.58-8 8-8Z" /></svg>
    </a>
</div>

Haz clic en el enlace de descarga de GitHub para controlar el sensor de seis ejes.

### Implementaci贸n del c贸digo

```cpp
#include <LSM6DS3.h>
#include <Wire.h>

// Crear una instancia de la clase LSM6DS3
LSM6DS3 myIMU(I2C_MODE, 0x6A);    // Direcci贸n del dispositivo I2C 0x6A
float aX, aY, aZ, gX, gY, gZ;
const float accelerationThreshold = 2.5; // umbral de aceleraci贸n significativa en G's
const int numSamples = 119;
int samplesRead = numSamples;

void setup() {
  // coloca tu c贸digo de configuraci贸n aqu铆, para que se ejecute una vez:
  Serial.begin(9600);
  while (!Serial);

  pinMode(PD5,OUTPUT);
  digitalWrite(PD5,HIGH);
  // Llamar a .begin() para configurar los IMUs
  if (myIMU.begin() != 0) {
    Serial.println("Error del dispositivo");
  } else {
    Serial.println("aX,aY,aZ,gX,gY,gZ");
  }
}

void loop() {
  // esperar a que ocurra un movimiento significativo
  while (samplesRead == numSamples) {
    // leer los datos de aceleraci贸n
    aX = myIMU.readFloatAccelX();
    aY = myIMU.readFloatAccelY();
    aZ = myIMU.readFloatAccelZ();

    // sumar los valores absolutos
    float aSum = fabs(aX) + fabs(aY) + fabs(aZ);

    // verificar si est谩 por encima del umbral
    if (aSum >= accelerationThreshold) {
      // restablecer el contador de muestras le铆das
      samplesRead = 0;
      break;
    }
  }

  // verificar si se han le铆do todas las muestras requeridas desde
  // la 煤ltima vez que se detect贸 un movimiento significativo
  while (samplesRead < numSamples) {
    // verificar si est谩n disponibles tanto los nuevos datos de aceleraci贸n como de giroscopio
    // leer los datos de aceleraci贸n y giroscopio

    samplesRead++;

    // imprimir los datos en formato CSV
    Serial.print(myIMU.readFloatAccelX(), 3);
    Serial.print(',');
    Serial.print(myIMU.readFloatAccelY(), 3);
    Serial.print(',');
    Serial.print(myIMU.readFloatAccelZ(), 3);
    Serial.print(',');
    Serial.print(myIMU.readFloatGyroX(), 3);
    Serial.print(',');
    Serial.print(myIMU.readFloatGyroY(), 3);
    Serial.print(',');
    Serial.print(myIMU.readFloatGyroZ(), 3);
    Serial.println();

    if (samplesRead == numSamples) {
      // a帽adir una l铆nea vac铆a si es la 煤ltima muestra
      Serial.println();
    }
  }
}
```
:::tip

Debido a la actualizaci贸n de la biblioteca LSM6DS3, si previamente la has a帽adido al usuario, necesitar谩s volver a descargar la versi贸n 2.0.4 o superior y agregar el archivo ZIP a Arduino.

:::
### Descripci贸n general de la funci贸n
- **Incluir bibliotecas**
  ```cpp
    #include <LSM6DS3.h> 
    #include <Wire.h>
  ```
  - Incluye la biblioteca para la comunicaci贸n con el sensor LSM6DS3.
  - Incluye la biblioteca para la comunicaci贸n I2C.

- **Crear Instancia del Sensor**
    - `LSM6DS3 myIMU(I2C_MODE, 0x6A)` Crea una instancia de la clase LSM6DS3 para el sensor IMU, especificando el modo de comunicaci贸n I2C y la direcci贸n del dispositivo 0x6A.

- **Variables y Constantes**
    - `float aX, aY, aZ, gX, gY, gZ`: Variables para almacenar los datos del aceler贸metro y giroscopio.
    - `const float accelerationThreshold = 2.5`: El valor umbral en G's para detectar un movimiento significativo.
    - `const int numSamples = 119`: El n煤mero de muestras a recolectar despu茅s de detectar un movimiento significativo.
    - `int samplesRead = numSamples`: Inicializa el contador de muestras al n煤mero total de muestras, indicando que a煤n no se ha recolectado ning煤n dato.

- **Configuraciones B谩sicas**
  ```cpp
    pinMode(PD5,OUTPUT);
    digitalWrite(PD5,HIGH);
  ```

  - Enciende el pin de habilitaci贸n de giroscopio.

- **Procesamiento de datos**
    ```cpp
    aX = myIMU.readFloatAccelX();:
    aY = myIMU.readFloatAccelY();:
    aZ = myIMU.readFloatAccelZ();:
    float aSum = fabs(aX) + fabs(aY) + fabs(aZ);
    ``` 

  - Lee la aceleraci贸n a lo largo del eje X.
  - Lee la aceleraci贸n a lo largo del eje Y.
  - Lee la aceleraci贸n a lo largo del eje Z.
  - Calcula la suma de los valores absolutos de los datos de aceleraci贸n, `fabs()` Devuelve el valor absoluto.

  ```cpp
    // Comprobar si est谩 por encima del umbral
    if (aSum >= accelerationThreshold) {
      // Restablecer el recuento de lectura de la muestra
      samplesRead = 0;
      break;
    }
  ```
  - - Si la suma de los valores absolutos de la aceleraci贸n es mayor o igual al umbral establecido, reinicia el contador de muestras `samplesRead` a 0 y sale del bucle.

- **Verificar datos**
  ```cpp
  while (samplesRead < numSamples) {
    samplesRead++;
    .
    .
    .
    .
    .
    if (samplesRead == numSamples) {
      // Agregue una l铆nea vac铆a si es la 煤ltima muestra
      Serial.println();
    }
  }
  ```

  - Ve a otro bucle y verifica si se ha le铆do el n煤mero requerido de muestras.
  - Incrementa el contador de samplesRead.
  - Si se han le铆do todas las muestras, imprime una l铆nea en blanco para separar la salida de datos.



### Resultados

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/six_resutl.png" style={{width:700, height:'auto'}}/></div>

### Mayor

Si deseas m谩s ejemplos de c贸digo, haz clic en: **"Archivo" -> Ejemplos -> Seeed Arduino LSM6DS3**

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/33.png" style={{width:500, height:'auto'}}/></div>



## Micr贸fono XIAO MG24 Sense

### Descripci贸n de los Sensores Integrados

**Sensor de Micr贸fono**, como el **MSM381ACT001**, es un micr贸fono MEMS (Micro-Electro-Mec谩nico) dise帽ado para capturar se帽ales de audio con **alta sensibilidad y bajo ruido**. Espec铆ficamente, el MSM381ACT001 tiene las siguientes caracter铆sticas:

**Funci贸n del Micr贸fono:**

- Captura las ondas sonoras y las convierte en se帽ales el茅ctricas, lo que permite la detecci贸n de entradas de audio en diversos entornos.
- Tiene un amplio rango de respuesta en frecuencia, t铆picamente de 20 Hz a 20 kHz, adecuado para diversas aplicaciones de audio, como el reconocimiento de voz y la reproducci贸n de m煤sica.

**Caracter铆sticas Clave:**

- Alta Sensibilidad: Capaz de detectar sonidos d茅biles, lo que lo hace ideal para aplicaciones que requieren una captura precisa de audio.
- Bajo Ruido: Dise帽ado para proporcionar una alta relaci贸n se帽al-ruido (SNR), garantizando una salida de audio clara incluso en entornos ruidosos.
- Tama帽o Compacto: La tecnolog铆a MEMS permite un factor de forma peque帽o, facilitando su integraci贸n en dispositivos port谩tiles como tel茅fonos inteligentes y dispositivos port谩tiles.
- Salida Digital: Ofrece opciones de salida de se帽al digital (por ejemplo, I2S), simplificando la interfaz con procesadores de se帽ales digitales (DSP) y microcontroladores.

### Preparaci贸n del Software


<div class="github_container" style={{textAlign: 'center'}}>
    <a class="github_item" href="https://github.com/Seeed-Studio/Seeed_Arduino_Mic">
    <strong><span><font color={'FFFFFF'} size={"4"}> Descargar las bibliotecas</font></span></strong> <svg aria-hidden="true" focusable="false" role="img" className="mr-2" viewBox="-3 10 9 1" width={16} height={16} fill="currentColor" style={{textAlign: 'center', display: 'inline-block', userSelect: 'none', verticalAlign: 'text-bottom', overflow: 'visible'}}><path d="M8 0c4.42 0 8 3.58 8 8a8.013 8.013 0 0 1-5.45 7.59c-.4.08-.55-.17-.55-.38 0-.27.01-1.13.01-2.2 0-.75-.25-1.23-.54-1.48 1.78-.2 3.65-.88 3.65-3.95 0-.88-.31-1.59-.82-2.15.08-.2.36-1.02-.08-2.12 0 0-.67-.22-2.2.82-.64-.18-1.32-.27-2-.27-.68 0-1.36.09-2 .27-1.53-1.03-2.2-.82-2.2-.82-.44 1.1-.16 1.92-.08 2.12-.51.56-.82 1.28-.82 2.15 0 3.06 1.86 3.75 3.64 3.95-.23.2-.44.55-.51 1.07-.46.21-1.61.55-2.33-.66-.15-.24-.6-.83-1.23-.82-.67.01-.27.38.01.53.34.19.73.9.82 1.13.16.45.68 1.31 2.69.94 0 .67.01 1.3.01 1.49 0 .21-.15.45-.55.38A7.995 7.995 0 0 1 0 8c0-4.42 3.58-8 8-8Z" /></svg>
    </a>
</div>

Haz clic en el enlace de descarga de GitHub para activar el sensor de micr贸fono.

:::tip
Actualmente, necesitamos modificar manualmente el archivo de reemplazo. La biblioteca para descarga directa estar谩 disponible pr贸ximamente. Por favor, espera la actualizaci贸n de nuestra wiki.
:::

- **[Archivos de Reemplazo]** [gsdk.a](https://files.seeedstudio.com/wiki/mg24_mic/gsdk_v2.a)

**Cambio de la ruta del archivo**
  - __/Users/tu_nombre/Library/Arduino15/packages/SiliconLabs/hardware/silabs/2.2.0/variants/xiao_mg24/ble_silabs/__

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/file.png" style={{width:350, height:'auto'}}/></div>


### Implementaci贸n del c贸digo

```cpp
#include <mic.h>
#if defined(WIO_TERMINAL)
#include "processing/filters.h"
#endif

// Configuraciones
#if defined(WIO_TERMINAL)
#define DEBUG 1                 // Habilitar el pulso del pin durante la ISR  
#define SAMPLES 16000*3
#elif defined(ARDUINO_ARCH_NRF52840)
#define DEBUG 1                 // Habilitar el pulso del pin durante la ISR  
#define SAMPLES 800
#elif defined(ARDUINO_SILABS)
#define DEBUG 1                 // Habilitar el pulso del pin durante la ISR  
#define SAMPLES 800
#endif

mic_config_t mic_config{
  .channel_cnt = 1,
  .sampling_rate = 16000,
  .buf_size = 1600,
#if defined(WIO_TERMINAL)
  .debug_pin = 1                // Alterna cada ISR de DAC (si DEBUG est谩 configurado en 1)
#elif defined(ARDUINO_ARCH_NRF52840)
  .debug_pin = LED_BUILTIN                // Alterna cada ISR de DAC (si DEBUG est谩 configurado en 1)
#elif defined(ARDUINO_SILABS)
  .debug_pin = LED_BUILTIN                // Alterna cada ISR de DAC (si DEBUG est谩 configurado en 1)  
#endif
};

#if defined(WIO_TERMINAL)
DMA_ADC_Class Mic(&mic_config);
#elif defined(ARDUINO_ARCH_NRF52840)
NRF52840_ADC_Class Mic(&mic_config);
#elif defined(ARDUINO_SILABS)
MG24_ADC_Class Mic(&mic_config);
#endif

int16_t recording_buf[SAMPLES];
volatile uint8_t recording = 0;
volatile static bool record_ready = false;

#if defined(WIO_TERMINAL)
FilterBuHp filter;
#endif

void setup() {

  Serial.begin(115200);
  while (!Serial) {delay(10);}
  
#if defined(WIO_TERMINAL)  
  pinMode(WIO_KEY_A, INPUT_PULLUP);
#endif

  Mic.set_callback(audio_rec_callback);

  if (!Mic.begin()) {
    Serial.println("Error de inicializaci贸n del micr贸fono");
    while (1);
  }

  Serial.println("Inicializaci贸n del micr贸fono completada.");

}

void loop() { 

#if defined(WIO_TERMINAL)  
if (digitalRead(WIO_KEY_A) == LOW && !recording) {

    Serial.println("Iniciando la toma de muestras");
    recording = 1;
    record_ready = false;  
}
#endif

#if defined(WIO_TERMINAL)  
  if (!recording && record_ready)
#elif defined(ARDUINO_ARCH_NRF52840) || defined(ARDUINO_SILABS)
  if (record_ready)
#endif  
  {
  Serial.println("Toma de muestras finalizada");
  
  for (int i = 0; i < SAMPLES; i++) {
    
  //int16_t sample = filter.step(recording_buf[i]);
  int16_t sample = recording_buf[i];
  Serial.println(sample);
  }
  
  record_ready = false; 
  }
}

static void audio_rec_callback(uint16_t *buf, uint32_t buf_len) {
  
  static uint32_t idx = 0;
  // Copiar las muestras del b煤fer DMA al b煤fer de inferencia
#if defined(WIO_TERMINAL)
  if (recording) 
#endif
  {
    for (uint32_t i = 0; i < buf_len; i++) {
  
      // Convertir el valor ADC sin signo de 12 bits a un valor de audio PCM de 16 bits (con signo)
#if defined(WIO_TERMINAL)
      recording_buf[idx++] = filter.step((int16_t)(buf[i] - 1024) * 16);
      //recording_buf[idx++] = (int16_t)(buf[i] - 1024) * 16;  
#elif defined(ARDUINO_ARCH_NRF52840) || defined(ARDUINO_SILABS)
      recording_buf[idx++] = buf[i];
#endif

      if (idx >= SAMPLES){ 
      idx = 0;
      recording = 0;
      record_ready = true;
      break;
     } 
    }
  }

}
```

### Function Overview




**Configuraci贸n de Micr贸fono**

  ```cpp
  mic_config_t mic_config{
  .channel_cnt = 1,
  .sampling_rate = 16000,
  .buf_size = 1600,
  #if defined(WIO_TERMINAL)
  .debug_pin = 1
  #elif defined(ARDUINO_ARCH_NRF52840)
  .debug_pin = LED_BUILTIN
  #elif defined(ARDUINO_SILABS)
  .debug_pin = LED_BUILTIN
  #endif
};
  ```

- **mic_config_t**: Define una estructura de configuraci贸n para el micr贸fono.
  - **channel_cnt**: Establecido en 1 para mono.
  - **sampling_rate**: Establecido en 16000 Hz para la frecuencia de muestreo.
  - **buf_size**: Establecido en 1600 para el tama帽o del b煤fer.
  - **debug_pin**: Establece el pin de depuraci贸n seg煤n la plataforma, utilizado para la indicaci贸n de se帽ales durante la depuraci贸n.

**Instanciaci贸n del micr贸fono**

  ```cpp
  #if defined(WIO_TERMINAL)
  DMA_ADC_Class Mic(&mic_config);
  #elif defined(ARDUINO_ARCH_NRF52840)
  NRF52840_ADC_Class Mic(&mic_config);
  #elif defined(ARDUINO_SILABS)
  MG24_ADC_Class Mic(&mic_config);
  #endif
  ```

- **Compilaci贸n condicional**: Crea las instancias adecuadas de la clase del micr贸fono para diferentes plataformas, utilizando la configuraci贸n previamente definida.

**B煤feres de grabaci贸n y banderas**

  ```cpp
  int16_t recording_buf[SAMPLES];
  volatile uint8_t recording = 0;
  volatile static bool record_ready = false;
  ```
  - `recording_buf`: Define un arreglo de SAMPLES para almacenar las muestras de grabaci贸n.
  - `recording`: una variable vol谩til que indica si la grabaci贸n est谩 en curso para evitar la optimizaci贸n por parte del compilador.
  - `record_ready`: una variable vol谩til est谩tica que indica si la grabaci贸n est谩 completa y lista para su procesamiento posterior.

**Ejemplo de Filtro (para WIO Terminal)**

  ```cpp
  #if defined(WIO_TERMINAL)
  FilterBuHp filter;
  #endif
  ```
  - Si est谩s en el WIO Terminal, crea una instancia de un filtro pasa-altos para el procesamiento del filtro.

**setup**
  ```cpp
  void setup() {
  Serial.begin(115200);
  while (!Serial) {delay(10);}
  
#if defined(WIO_TERMINAL)  
  pinMode(WIO_KEY_A, INPUT_PULLUP);
#endif

  Mic.set_callback(audio_rec_callback);

  if (!Mic.begin()) {
    Serial.println("Inicializaci贸n de micr贸fono fallida.");
    while (1);
  }

  Serial.println("Micr贸fono inicializado.");
}
  ```

  - Inicializar el Puerto Serial: Inicia la comunicaci贸n serial a una velocidad de 115200 baudios y espera a que el puerto serial est茅 listo.
  - Configurar el Modo de Pin: En el WIO Terminal, configura los pines clave en modo de entrada con pull-up.
  - Establecer la funci贸n de callback: llama a Mic.set_callback(audio_rec_callback) para especificar la funci贸n de callback cuando se grabe audio.
  - Inicializar el micr贸fono: llama a Mic.begin(), si la inicializaci贸n falla, imprime un mensaje de error y entra en un bucle muerto.

**loop**

  ```cpp
  void loop() { 
#if defined(WIO_TERMINAL)  
if (digitalRead(WIO_KEY_A) == LOW && !recording) {
    Serial.println("Empezando muestreo");
    recording = 1;
    record_ready = false;  
}
#endif

#if defined(WIO_TERMINAL)  
  if (!recording && record_ready)
#elif defined(ARDUINO_ARCH_NRF52840) || defined(ARDUINO_SILABS)
  if (record_ready)
#endif  
  {
    Serial.println("Muestreo finalizado");
    
    for (int i = 0; i < SAMPLES; i++) {
      int16_t sample = recording_buf[i];
      Serial.println(sample);
    }
    
    record_ready = false; 
  }
}
  ```

  - Detectar Tecla: En el WIO Terminal, comienza a grabar cuando detecta que se ha presionado una tecla y no est谩 grabando.
  - Muestreo Finalizado: Imprime "Muestreo finalizado" si no se est谩 grabando y `record_ready` est谩 establecido en `true`.
  - Itera a trav茅s del buffer de grabaci贸n e imprime el valor de cada muestra.

**Funci贸n de callback de grabaci贸n de audio**

  ```cpp
  static void audio_rec_callback(uint16_t *buf, uint32_t buf_len) {
  static uint32_t idx = 0;
  #if defined(WIO_TERMINAL)
  if (recording) 
  #endif
  {
    for (uint32_t i = 0; i < buf_len; i++) {
      #if defined(WIO_TERMINAL)
      recording_buf[idx++] = filter.step((int16_t)(buf[i] - 1024) * 16);
      #elif defined(ARDUINO_ARCH_NRF52840) || defined(ARDUINO_SILABS)
      recording_buf[idx++] = buf[i];
      #endif

      if (idx >= SAMPLES){ 
        idx = 0;
        recording = 0;
        record_ready = true;
        break;
      } 
    }
  }
}
  ```

  - Funci贸n de Callback: Se llama durante la grabaci贸n de audio, responsable de copiar las muestras desde el buffer DMA al buffer de grabaci贸n.
  - Compilaci贸n Condicional: Procesa la entrada utilizando filtros si se est谩 utilizando el WIO Terminal.
  - Convierte los valores ADC sin signo de 12 bits a valores de audio PCM de 16 bits (con signo).
  - Llenado de Muestras: Copia las muestras en `recording_buf` y actualiza el 铆ndice `idx`.
  - Finalizar Grabaci贸n: Si el n煤mero de muestras llenadas alcanza `SAMPLES`, reinicia el 铆ndice, marca el final de la grabaci贸n y establece `record_ready` en `true`.


### Resultados


<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/mic_result1.png" style={{width:680, height:'auto'}}/></div>

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/mic_result.png" style={{width:680, height:'auto'}}/></div>

Aqu铆 est谩 la forma de onda del sonido reconocido, cuando soplas, se puede ver claramente que la amplitud de la oscilaci贸n de la forma de onda se hace m谩s grande.

### M谩s

Si deseas m谩s ejemplos de c贸digo, haz clic en: -> **"Ejemplo -> Seeed Arduino Mic"**

<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/34.png" style={{width:500, height:'auto'}}/></div>
<div style={{textAlign:'center'}}><img src="https://files.seeedstudio.com/wiki/mg24_mic/35.png" style={{width:500, height:'auto'}}/></div>


## Recursos

### Para Seeed Studio XIAO MG24 Sense

-  **[PDF]** [Seeed Studio 6-Axis IMU(LSM6DS3TR-C) datasheet](https://statics3.seeedstudio.com/fusion/opl/sheets/314040211.pdf)
-  **[PDF]** [Seeed Studio Analog Microphone(MSM381ACT001) datasheet](https://files.seeedstudio.com/wiki/mg24_mic/312030602_MEMSensing_MSM381ACT001_Datasheet.pdf)

## Soporte T茅cnico y Discusi贸n de Productos

隆Gracias por elegir nuestros productos! Estamos aqu铆 para ofrecerte diferentes tipos de soporte y garantizar que tu experiencia con nuestros productos sea lo m谩s fluida posible. Ofrecemos varios canales de comunicaci贸n para adaptarnos a tus preferencias y necesidades.

<div class="button_tech_support_container">
<a href="https://forum.seeedstudio.com/" class="button_forum"></a>
<a href="https://www.seeedstudio.com/contacts" class="button_email"></a>
</div>

<div class="button_tech_support_container">
<a href="https://discord.gg/eWkprNDMU7" class="button_discord"></a>
<a href="https://github.com/Seeed-Studio/wiki-documents/discussions/69" class="button_discussion"></a>
</div>
